AC_INIT(hildon-fm/hildon-file-system-model.c)
AM_INIT_AUTOMAKE(libhildonfm, 2.28)
AM_CONFIG_HEADER(config.h)

AC_CANONICAL_HOST

AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_AWK
AC_PROG_LIBTOOL
AC_HEADER_STDC

AC_ARG_ENABLE([debug],
              AS_HELP_STRING([--enable-debug], [compile with debug messages [[default=no]]]),
	      [], [enable_debug=no])
AS_IF([test x$enable_debug != xno],
      [DEBUG_CPPFLAGS=],
      [DEBUG_CPPFLAGS=-DG_DEBUG_DISABLE])
AC_SUBST([DEBUG_CPPFLAGS])

WARN_CFLAGS="-Wall -Wmissing-prototypes -Wmissing-declarations -Wno-format-extra-args"
AC_SUBST([WARN_CFLAGS])

GTK_VERSION=2.14.0

PKG_CHECK_MODULES(GTK, gtk+-2.0 >= $GTK_VERSION)
AC_SUBST(GTK_LIBS)
AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_VERSION)

PKG_CHECK_MODULES(HILDON, hildon-1 >= 2.2.21)
AC_SUBST(HILDON_LIBS)
AC_SUBST(HILDON_CFLAGS)

# Needed to query the paired gateway device

PKG_CHECK_MODULES(GCONF, gconf-2.0 >= 2.6)
AC_SUBST(GCONF_CFLAGS)
AC_SUBST(GCONF_LIBS)

PKG_CHECK_MODULES(HILDON_THUMBNAIL, hildon-thumbnail >= 0.0.4)
AC_SUBST(HILDON_THUMBNAIL_CFLAGS)
AC_SUBST(HILDON_THUMBNAIL_LIBS)

PKG_CHECK_MODULES(LIBOSSOMIME, libhildonmime >= 1.9)
AC_SUBST(LIBOSSOMIME_CFLAGS)
AC_SUBST(LIBOSSOMIME_LIBS)

# Updated required version. Old 0.23.1 is not compatible with 0.23.4
PKG_CHECK_MODULES(DBUSGLIB, dbus-glib-1 >= 0.23.4)
AC_SUBST(DBUSGLIB_CFLAGS)
AC_SUBST(DBUSGLIB_LIBS)

# We need only macros from libosso, not libraries
PKG_CHECK_MODULES(LIBOSSO, libosso >= 0.8.8)
AC_SUBST(LIBOSSO_CFLAGS)

PKG_CHECK_MODULES(MCE, mce >= 0.8.5)
AC_SUBST(MCE_CFLAGS)

PKG_CHECK_MODULES(GIO, gio-2.0 gio-unix-2.0)
AC_SUBST(GIO_CFLAGS)
AC_SUBST(GIO_LIBS)

#PKG_CHECK_MODULES(TRACKER, tracker)
#AC_SUBST(TRACKER_CFLAGS)
#AC_SUBST(TRACKER_LIBS)

##################################################
# Check for gtk-doc.
##################################################
GTK_DOC_CHECK


# Disable rebuild of glib-mkenum -generated source code:
AC_ARG_ENABLE(rebuilds, [  --disable-rebuilds      disable all source autogeneration rules],,enable_rebuilds=yes)

# define a MAINT-like variable REBUILD which is set if Perl
# is found, so autogenerated sources can be rebuilt
# Perl is needed by glib-mkenums
AC_CHECK_PROGS(PERL, perl5 perl)

GLIB_MKENUMS=`$PKG_CONFIG --variable=glib_mkenums glib-2.0`
AC_SUBST(GLIB_MKENUMS)

REBUILD=\#
if test "x$enable_rebuilds" = "xyes" && \
     test -n "$PERL" && \
     $PERL -e 'exit !($] >= 5.002)' > /dev/null 2>&1 ; then
  REBUILD=
fi
AC_SUBST(REBUILD)

GETTEXT_PACKAGE="hildon-fm"
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [gettext package name])

AC_CONFIG_FILES([Makefile
		 hildon-fm/Makefile
		 hildon-fm/gtkfilesystem/Makefile
		 data/user-dirs.defaults
 	         doc/Makefile
		 tests/Makefile
	         hildon-fm-2.pc
	         hildon-fm.pc])

AC_OUTPUT
